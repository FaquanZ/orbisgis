options {
	STATIC = false ;
}
PARSER_BEGIN(SQLEngine)
package org.gdms.sql.parser;
import java.io.FileInputStream;
import java.io.FileNotFoundException;

public class SQLEngine {
	public static void main( String[] args )
		throws ParseException, TokenMgrError, FileNotFoundException {
			SQLEngine parser = new SQLEngine(new FileInputStream(args[0]));
			parser.SQLStatement();
	}

	private void jjtreeOpenNodeScope(Node node) {
		((SimpleNode)node).first_token = getToken(1);
	}

	private void jjtreeCloseNodeScope(Node node ) {
		((SimpleNode)node).last_token = getToken(0);
	}

	public Node getRootNode(){
		return jjtree.rootNode();
	}

}
PARSER_END(SQLEngine)

/***********************************************************************
 * Token definitions
 ***********************************************************************/

SKIP:
{
    " "
  | "\n"
  | "\r"
  | "\t"
}


SKIP:
{
  <COMMENT_LINE: "--" (~["\n","\r"])* ("\n"|"\r"|"\r\n") >
}


SKIP:
{
  <COMMENT_BLOCK: "/*" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
}


TOKEN:  /* Keywords */
{
    < ALL: "all" >
  | < AND: "and" >
  | < AS: "as" >
  | < ASC: "asc" >
  | < BEGIN: "begin" >
  | < BETWEEN: "between" >
  | < BY: "by" >
  | < DESC: "desc" >
  | < DISTINCT: "distinct" >
  | < DROP: "drop" >
  | < FROM: "from" >
  | < GROUP: "group" >
  | < HAVING: "having" >
  | < IN: "in" >
  | < IS: "is" >
  | < KEY: "key" >
  | < LIKE: "like" >
  | < LIMIT: "limit" >
  | < NOT: "not" >
  | < NULL: "null" >
  | < OFFSET: "offset" >
  | < OR: "or" >
  | < ORDER: "order" >
  | < PRIMARY: "primary" >
  | < SELECT: "select" >
  | < UNION: "union" >
  | < SPACES: "spaces" >
  | < TABLE: "table" >
  | < WHERE: "where" >

//***************************************************************************************
  | < CREATE: "create" >
  | < DELETE: "delete" >
  | < EXISTS: "exists" >
  | < INSERT: "insert" >
  | < INTO: "into" >
  | < SET: "set" >
  | < UPDATE: "update" >
  | < VALUES: "values" >
}


TOKEN:  /* Literals */
{
    < INTEGER_LITERAL: (["0"-"9"])+ >
  | < FLOATING_POINT_LITERAL:
          (["0"-"9"])+ "." (["0"-"9"])+ (<EXPONENT>)?
        | "." (["0"-"9"])+ (<EXPONENT>)?
        | (["0"-"9"])+ <EXPONENT>
        | (["0"-"9"])+ (<EXPONENT>)?
    >
  | < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
  | < STRING_LITERAL: "'" (~["'"])* ( "''" (~["'"])* )* "'" >
  | < BOOLEAN_LITERAL: "true" | "false" >
}


TOKEN:  /* Identifiers */
{
    < ID: ( <LETTER> )+ ( "_" | "$" | "#" | <DIGIT> | <LETTER> )* >
  | < #LETTER: ["A"-"Z", "a"-"z"] >
  | < #DIGIT: ["0"-"9"] >
  | < QUOTED_ID: "\"" (~["\n","\r","\""])* "\"" >
}


TOKEN:  /* Separators and operators */
{
    < ASSIGN: ":=" >
  | < CONCAT: "||" >
  | < SEMICOLON: ";" >
  | < DOT: "." >
  | < TILDE: "~" >
  | < LESS: "<" >
  | < LESSEQUAL: "<=" >
  | < GREATER: ">" >
  | < GREATEREQUAL: ">=" >
  | < EQUAL: "=" >
  | < NOTEQUAL: "!=" >
  | < NOTEQUAL2: "<>" >
  | < JOINPLUS: "(+)" >
  | < OPENPAREN: "(" >
  | < CLOSEPAREN: ")" >
  | < ASTERISK: "*" >
  | < SLASH: "/" >
  | < PLUS: "+" >
  | < MINUS: "-" >
  | < QUESTIONMARK: "?" >
}



void SQLAndExpr() :
{}
{
  SQLNotExpr()
  ( LOOKAHEAD(2) <AND>
	 SQLNotExpr() )*
}

void SQLBetweenClause() :
{}
{
  [ <NOT> ]
  <BETWEEN>
  SQLSumExpr()
  <AND>
  SQLSumExpr()
}

void SQLColRef() :
{
}
{
  SQLId()
  [ <DOT> SQLId() ]
}

void SQLCompareExpr() :
{
}
{
  (
    LOOKAHEAD (SQLSumExpr() <IS>) SQLIsClause()
  | SQLExistsClause()
  | SQLSumExpr() [ SQLCompareExprRight() ]
  )
}

void SQLCompareExprRight() :
{}
{
  (
    LOOKAHEAD(2) SQLLikeClause()
  | LOOKAHEAD(2) SQLInClause()
  | SQLLeftJoinClause()
  | LOOKAHEAD(SQLCompareOp() SQLSumExpr() "(+)") SQLRightJoinClause()
  | SQLBetweenClause()
  | SQLCompareOp()
    SQLSumExpr()
  )
}

void SQLCompareOp() :
{}
{
  (
    <EQUAL>
  | <NOTEQUAL>
  | <NOTEQUAL2>
  | <GREATER>
  | <GREATEREQUAL>
  | <LESS>
  | <LESSEQUAL>
  )
}

void SQLDelete() :
{}
{
  <DELETE> <FROM>
  SQLId()
  [ SQLWhere() ]
}

void SQLDrop() :
{}
{
  <DROP> <TABLE>
  SQLTableList()
}
void SQLExistsClause() :
{}
{
  <EXISTS>
  "(" SQLSelect() ")"
}

void SQLFunction() :
{}
{
  <ID>
    SQLFunctionArgs()
}

void SQLFunctionArgs() :
{}
{
  "("
    [
      (
        (SQLSumExpr() ( "," SQLSumExpr() )* )
        |
        SQLSelectAllCols()
      )
    ]
  ")"
}

void SQLGroupBy() :
{}
{
  <GROUP> <BY>
  SQLGroupByList()
  [<HAVING> SQLOrExpr()]
}

void SQLGroupByList() :
{}
{
  SQLColRef()
  (LOOKAHEAD(2) "," SQLColRef())*
}

void SQLId() :
{}
{
  <ID> | <QUOTED_ID>
}

void SQLInClause() :
{}
{
  [ <NOT> ]
  <IN>
  "("
  SQLValueList()
  ")"
}

void SQLInsert() :
{}
{
  <INSERT> <INTO>
  SQLTableRef()
  [
    "("
    SQLId()
    ("," SQLId())*
    ")"
  ]
  <VALUES>
  "("
    SQLSumExpr()
    ("," SQLSumExpr())*
  ")"
}

void SQLIsClause() :
{}
{
  SQLSumExpr()
  <IS>
  [ <NOT>  ]
  <NULL>
}

void SQLLeftJoinClause() :
{
}
{
  "(+)" SQLCompareOp()
  SQLSumExpr()
}

void SQLLikeClause() :
{}
{
  [ <NOT>  ]
  <LIKE>
  SQLPattern()
}

void SQLLiteral() :
{
}
{
  ( <STRING_LITERAL>
  | <INTEGER_LITERAL>
  | <FLOATING_POINT_LITERAL>
  | <BOOLEAN_LITERAL>
  | <SPACES>
  )
}

void SQLNotExpr() :
{}
{
  [ <NOT> ]
  SQLCompareExpr()
}

void SQLOrderBy() :
{}
{
  <ORDER> <BY>
  SQLOrderByList()
}

void SQLOrderByElem() :
{}
{
  SQLColRef()
  [ SQLOrderDirection()]
}

void SQLOrderByList() :
{}
{
  SQLOrderByElem()
  ( LOOKAHEAD(2)
    ","
    SQLOrderByElem() )*
}

void SQLOrderDirection() :
{}
{
  (
    <ASC>
  | <DESC>
  )
}

void SQLOrExpr() :
{}
{
  SQLAndExpr()
  ( LOOKAHEAD(2) <OR>
	SQLAndExpr() )*
}

void SQLPattern() :
{
}
{
  ( <STRING_LITERAL>
  | SQLColRef()
  )
}

void SQLProductExpr() :
{}
{
  SQLUnaryExpr()
  ( SQLProductSymbol() SQLUnaryExpr())*
}

void SQLProductSymbol() :
{}
{
	( "*" | "/")
}

void SQLRightJoinClause() :
{
}
{
  SQLCompareOp()
  SQLSumExpr()
  "(+)"
}

void SQLScript() :
{}
{
	SQLStatement() (SQLStatement())*
}

void SQLSelect() :
{}
{
  <SELECT>
  SQLSelectCols()
  [<FROM>
    SQLTableList()
    [ SQLWhere()]
    [ LOOKAHEAD(2) SQLGroupBy()]
    [ LOOKAHEAD(2) SQLOrderBy()]
    [ LOOKAHEAD(2) SQLSelectLimit()]
    [ LOOKAHEAD(2) SQLSelectOffset() ]
  ]
}

void SQLSelectAllCols() :
{}
{
  "*"
}

void SQLSelectAllColsInTable() :
{}
{
  SQLId() <DOT> "*"
}

void SQLSelectCols() :
{}
{
  [
    <ALL>
    | <DISTINCT>
  ]
  (SQLSelectList())
}

void SQLSelectLimit() :
{}
{
	<LIMIT> <INTEGER_LITERAL>
}

void SQLSelectList() :
{}
{
  (
    SQLSelectAllCols()
    | LOOKAHEAD(3) SQLSelectAllColsInTable()
    | (SQLOrExpr() [<AS> SQLId()])
  )
  (LOOKAHEAD(2) ","
    (
      SQLSelectAllCols()
      | LOOKAHEAD(3) SQLSelectAllColsInTable()
      | (SQLOrExpr() [<AS> SQLId()])
    )
  )*
}

void SQLSelectOffset() :
{}
{
	<OFFSET> <INTEGER_LITERAL>
}

void SQLStatement() :
{}
{
  (
    SQLSelect()
  | SQLInsert()
  | SQLUpdate()
  | SQLDelete()
  | SQLUnion()
  | SQLCreate()
  | SQLDrop()
  ) ";"
}

void SQLSumExpr() :
{}
{
    SQLProductExpr()
     ( SQLSumSymbol() SQLProductExpr()  )*
}

void SQLSumSymbol() :
{}
{
	( "+" | "-")
}

void SQLTableList() :
{}
{
  SQLTableRef()
  ( LOOKAHEAD(2) "," SQLTableRef() )*
}


void SQLTableRef() :
{
}
{
  (SQLId()) [ [<AS>] SQLId() ]
}

void SQLTerm() :
{
}
{
  (
    "("
    SQLOrExpr()
    ")"
  | LOOKAHEAD(2) SQLFunction()
  | SQLColRef()
  | SQLLiteral()
  )
}

void SQLUnaryExpr() :
{}
{
  [ ( "+"
    | "-"
    )
  ] SQLTerm()
}

void SQLUnion() :
{}
{
	(
	SQLTableRef()
	|
	<OPENPAREN>
	SQLSelect()
	<CLOSEPAREN>
	)
	<UNION>
	(
	SQLTableRef()
	|
	<OPENPAREN>
	SQLSelect()
	<CLOSEPAREN>
	)
}

void SQLUpdate() :
{}
{
  <UPDATE>
  SQLId()
  <SET>
  (
    SQLUpdateAssignment()
    [
      ","
    ]
  )+

  [ SQLWhere() ]
}

void SQLUpdateAssignment() :
{}
{
  SQLId() "=" SQLSumExpr()
}

void SQLLValueElement() :
{}
{
  ( <NULL>
  | SQLSumExpr()
  | SQLSelect())
}

void SQLValueList() :
{}
{
  SQLLValueElement()
  ( ","
    SQLLValueElement()
  )*
}

void SQLWhere() :
{}
{
  <WHERE> SQLOrExpr()
}

void SQLCreate() :
{}
{
  <CREATE> <TABLE> SQLId()
  (
  "(" SQLCreateArgsList() ")" | <AS> (SQLSelect() | SQLUnion())
  )
}

void SQLCreateArgsList() :
{}
{
	SQLColumnDefinition() (LOOKAHEAD(2) "," SQLColumnDefinition() ) *
	[ "," <PRIMARY> <KEY> "(" SQLId() ( "," SQLId() ) * ")" ]
}

void SQLColumnDefinition() :
{}
{
    SQLId() <ID> "(" [ SQLDataTypeConstraint() ( "," SQLDataTypeConstraint() ) * ] ")"
}

void SQLDataTypeConstraint() :
{}
{
    <ID> "(" [ SQLTypeArgs() ] ")"
}

void SQLTypeArgLiteral() :
{
}
{
  ( <STRING_LITERAL>
  | <INTEGER_LITERAL>
  | <FLOATING_POINT_LITERAL>
  )
}

void SQLTypeArgs() :
{}
{
	(
	(SQLTypeArgLiteral()
	| SQLFunction())
	( "," (SQLTypeArgLiteral()
	| SQLFunction()) )*

	)
}

